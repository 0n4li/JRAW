apply plugin: 'org.jetbrains.dokka'
apply plugin: 'net.ltgt.apt-idea'
apply plugin: 'maven'

ext.projectName = 'JRAW'

dependencies {
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // TODO: Use jitpack for Moshi until square/moshi#349 and #350 are merged to handle deeply nested objects
//    compile "com.squareup.moshi:moshi:$moshiVersion"
    def moshi = "com.github.mattbdean.moshi:moshi:349-deeply-nested-value-reader-SNAPSHOT"
    compile(moshi) {
        exclude group: 'com.squareup.moshi'
    }

    apt('com.github.rharter:auto-value-moshi:4a4423b') {
        exclude group: 'com.squareup.moshi'
    }
    apt moshi
    apt "com.google.auto.value:auto-value:$autoValueVersion"

    compileOnly "com.google.auto.value:auto-value:$autoValueVersion"
    compileOnly('com.ryanharter.auto.value:auto-value-moshi-annotations:0.4.3') {
        exclude group: 'com.squareup.moshi'
    }

    testCompile "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testCompile 'net.sourceforge.htmlunit:htmlunit:2.27'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

sourceSets {
    gen // Generated Java code (see the :meta project)
    main {
        java {
            // Include generated sources
            source gen.allSource
        }
    }
}

dokka {
    moduleName = 'jraw'
    jdkVersion = 7
    impliedPlatforms = ['JVM']
    reportNotDocumented = true
    outputFormat = 'javadoc'

    linkMapping {
        dir = file('src/main/kotlin')
        url = 'https://github.com/thatJavaNerd/JRAW/tree/master/lib/src/main/kotlin'
        suffix = '#L'
    }
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
    baseName = projectName
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    baseName = projectName
}

jar {
    baseName = projectName
}

// When JitPack builds, it runs the install task (from the maven plugin) to install all artifacts listed here to the
// local repository. List the sources and Javadoc jar here to make it available to JitPack users.
artifacts {
    archives sourcesJar
    archives javadocJar
}

task jars(dependsOn: [javadocJar, sourcesJar])
