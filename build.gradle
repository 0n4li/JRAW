import java.util.stream.Collectors

buildscript {
    ext.kotlinVersion = '1.1.2-5'
    ext.spekVersion = '1.1.2'
    ext.jacksonVersion = '2.9.0.pr3'
    ext.dokkaVersion = '0.9.14'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
}
apply plugin: 'jacoco'

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

subprojects {
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
    group = 'net.dean.jraw'
    version = '0.9.0'

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/jetbrains/spek' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testCompile 'com.winterbe:expekt:0.2.0'
        testCompile ("org.jetbrains.spek:spek-api:$spekVersion") {
            exclude group: 'org.jetbrains.kotlin'
        }
        testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M4'
        testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }
    }

    compileKotlin {
        kotlinOptions {
            apiVersion = "1.1"
            languageVersion = "1.1"
        }
    }

    project.afterEvaluate {
        def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

        jacoco {
            toolVersion = "0.7.9"
            reportsDir = file("$buildDir/reports/jacoco")
            applyTo junitPlatformTestTask
        }

        project.task(type: JacocoReport, "junitPlatformJacocoReport", {
            sourceDirectories = files("src/main/kotlin")
            classDirectories = files("$buildDir/classes/main")
            reports {
                xml.enabled = true
                html.enabled = true
                xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
                html.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.html")
            }
            executionData junitPlatformTestTask
        })
    }

    junitPlatform {
        filters {
            engines {
                include 'spek'
            }
        }
    }

    coveralls {
        sourceDirs += ['src/main/kotlin']
    }
}

// Only do code coverage for :lib
def reportedProjects = subprojects.findAll { it.name == "lib" }
def subprojectsExecFiles = files(reportedProjects.stream().map { "${it.name}/build/jacoco/junitPlatformTest.exec" }.collect(Collectors.toList()) )

task jacocoRootReport(type: JacocoReport) {
    additionalSourceDirs = files(reportedProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(reportedProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(reportedProjects.sourceSets.main.output)
    executionData = subprojectsExecFiles
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    onlyIf = {
        true
    }
}
