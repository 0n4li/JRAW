buildscript {
    ext.kotlinVersion = '1.1.2-2'
    ext.spekVersion = '1.1.2'
    ext.jacksonVersion = '2.9.0.pr3'
    ext.dokkaVersion = '0.9.14'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
    }
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'
apply plugin: 'kotlin'

repositories {
    jcenter()
    maven { url 'https://d.bintray.com/jetbrains/spek' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    // Required since we manually excluded org.jetbrains.kotlin from spek
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testCompile 'com.winterbe:expekt:0.2.0'
    testCompile ("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M4'
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile 'net.sourceforge.htmlunit:htmlunit:2.27'
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    jacoco {
        toolVersion = "+"
        reportsDir = file("$buildDir/reports/jacoco")
        applyTo junitPlatformTestTask
    }

    project.task(type: JacocoReport, "junitPlatformJacocoReport", {
        sourceDirectories = files("src/main/kotlin")
        classDirectories = files("$buildDir/classes/main")
        reports {
            xml.enabled = true
            html.enabled = true
        }
        executionData junitPlatformTestTask
    })
}

